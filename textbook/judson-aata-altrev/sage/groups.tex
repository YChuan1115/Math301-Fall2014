%%%%(c)
%%%%(c)  This file is a portion of the source for the textbook
%%%%(c)
%%%%(c)    Abstract Algebra: Theory and Applications
%%%%(c)    by Thomas W. Judson
%%%%(c)
%%%%(c)    Sage Material
%%%%(c)    Copyright 2011 by Robert A. Beezer
%%%%(c)
%%%%(c)  See the file COPYING.txt for copying conditions
%%%%(c)
%%%%(c)
Many of the groups discussed in this chapter are available for study in Sage.  It is important to understand that sets that form algebraic objects (groups in this chapter) are called ``parents'' in Sage, and elements of these objects are called, well, ``elements.''  So every element belongs to a parent (in other words, is contained in some set).  We can ask about properties of parents (finite? order? abelian?), and we can ask about properties of individual elements (identity? inverse?).  In the following we will show you how to create some of these common groups and begin to explore their properties with Sage.
%
\sagesubsection{Integers mod n}
%
\begin{sageexample}
sage: Z8 = Integers(8)
sage: Z8
Ring of integers modulo 8
sage: Z8.list()
[0, 1, 2, 3, 4, 5, 6, 7]
sage: a = Z8.an_element(); a
1
sage: a.parent()
Ring of integers modulo 8
\end{sageexample}
%
We would like to work with elements of \verb?Z8?.  If you were to type a \verb?6? into a compute cell right now, what would you mean?  The integer $6$, the rational number $\frac{6}{1}$, the real number $6.00000$, or the complex number $6.00000+0.00000i$?  Or perhaps you really do want the integer $6$ mod $8$?  Sage really has no idea what you mean or want.  To make this clear, you can ``coerce'' \verb?6? into \verb?Z8? with the syntax \verb?Z8(6)?.  Without this, Sage will treat a input number like \verb?6? as an integer, the simplest possible interpretation in some sense.  Study the following carefully, where we first work with ``normal'' integers and then with integers mod 8.
%
\begin{sageexample}
sage: a = 6
sage: a
6
sage: a.parent()
Integer Ring
sage: b = 7
sage: c = a + b; c
13
sage: d = Z8(6)
sage: d
6
sage: d.parent()
Ring of integers modulo 8
sage: e = Z8(7)
sage: f = d+e; f
5
sage: g = Z8(85); g
5
sage: f == g
True
\end{sageexample}
%
\verb?Z8? is a bit unusual as a first example, since it has two operations defined, both addition and multiplication, with addition forming a group, and multiplication not forming a group.  Still, we can work with the additive portion, here forming the Cayley table for the addition.
%
\begin{sageexample}
sage: Z8.addition_table(names='elements')
+  0 1 2 3 4 5 6 7
 +----------------
0| 0 1 2 3 4 5 6 7
1| 1 2 3 4 5 6 7 0
2| 2 3 4 5 6 7 0 1
3| 3 4 5 6 7 0 1 2
4| 4 5 6 7 0 1 2 3
5| 5 6 7 0 1 2 3 4
6| 6 7 0 1 2 3 4 5
7| 7 0 1 2 3 4 5 6
\end{sageexample}
%
When $n$ is a prime number, the multipicative structure (excluding zero), will also form a group.
%
The integers mod $n$ are very important, so Sage implements both addition and multiplication together.  Groups of symmetries are a better example of how Sage implements groups, since there is just one operation present.\par
%
\sagesubsection{Groups of symmetries}
%
The symmetries of some geometric shapes are already defined in Sage, albeit with different names.  They are implemented as ``permutation groups'' which we will begin to study carefully in Chapter~\ref{permute}.\par
%
Sage uses integers to label vertices, starting the count at \verb?1?, instead of letters.  Elements by default are printed using ``cycle notation'' which we will see described carefully in Chapter~\ref{permute}.    Here is an example, with both the mathematics and Sage.  For the Sage part, we create the group of symmetries and then create the symmetry $\rho_2$ with coercion, followed by outputting the element in cycle notation.  Then we create just the \emph{bottom row} of the notation we are using for permutations.
%
\begin{align*}
\rho_2&=
\begin{pmatrix}
A & B & C\\
C & A & B
\end{pmatrix}
=
\begin{pmatrix}
1 & 2 & 3\\
3 & 1 & 2
\end{pmatrix}
\end{align*}
%
\begin{sageexample}
sage: triangle = SymmetricGroup(3)
sage: rho2 = triangle([3,1,2])
sage: rho2
(1,3,2)
sage: [rho2(x) for x in triangle.domain()]
[3, 1, 2]
\end{sageexample}
%
The final list comprehension deserves comment.  The \verb?.domain()? method gives a lait of the symbols used for the permutation group \verb?triangle? and then \verb?rho2? is employed with syntax like it is a function (it \emph{is} a function) to create the images that would occupy the bottom row.\par
%
With a double list comprehension we can list all six elements of the group in the ``bottom row'' format.  A good exercise would be to pair up each element with its name as given in Figure~\ref{S3_Symmetry_Figure}.
%
\begin{sageexample}
sage: [[a(x) for x in triangle.domain()] for a in triangle]
[[1, 2, 3], [1, 3, 2], [2, 1, 3], [2, 3, 1], [3, 1, 2], [3, 2, 1]]
\end{sageexample}
%
Different books, different authors, different software all have different ideas about the order in which to write multiplication of functions.  This textbook builds on the idea of composition of functions, so that $fg$ is the composition $(fg)(x)=f(g(x))$ and it is natural to apply $g$ first.  Sage takes the opposite view and since we write $fg$, Sage will understand that we want to do $f$ first.  Neither approach is wrong, and neither is necessarily superior, they are just different and there are good arguments for either one.  When you consult other books that work with permutation groups, you want to first determine which approach it takes.\par
%
The translation here between the text and Sage will be worthwhile practice.  Here we will reprise the discussion at the end of Section 3.1, but reverse the order on each product to compute Sage-style and exactly mirror what the text does.
%
\begin{sageexample}
sage: mu1 = triangle([1,3,2])
sage: mu2 = triangle([3,2,1])
sage: mu3 = triangle([2,1,3])
sage: rho1 = triangle([2,3,1])
sage: product = rho1*mu1
sage: product == mu2
True
sage: [product(x) for x in triangle.domain()]
[3, 2, 1]
sage: rho1*mu1 == mu1*rho1
False
sage: mu1*rho1 == mu3
True
\end{sageexample}
%
Now that we understand that Sage does multiplication in reverse, we can compute the Cayley table for this group.  Default behavior is to just name elements of a group as letters, \verb?a, b, c?\dots in the same order that the \verb?.list()? command would produce the elements of the group.  But you can also print the elements in the table as themselves (that uses cycle notation here), or you can give the elements names.  We will use ``u`` as shorthand for $\mu$ and ``r'' as shorthand for $\rho$.
%
\begin{sageexample}
sage: triangle.cayley_table()
*  a b c d e f
 +------------
a| a b c d e f
b| b a d c f e
c| c e a f b d
d| d f b e a c
e| e c f a d b
f| f d e b c a
sage: triangle.cayley_table(names='elements')
      *       ()   (2,3)   (1,2) (1,2,3) (1,3,2)   (1,3)
       +------------------------------------------------
     ()|      ()   (2,3)   (1,2) (1,2,3) (1,3,2)   (1,3)
  (2,3)|   (2,3)      () (1,2,3)   (1,2)   (1,3) (1,3,2)
  (1,2)|   (1,2) (1,3,2)      ()   (1,3)   (2,3) (1,2,3)
(1,2,3)| (1,2,3)   (1,3)   (2,3) (1,3,2)      ()   (1,2)
(1,3,2)| (1,3,2)   (1,2)   (1,3)      () (1,2,3)   (2,3)
  (1,3)|   (1,3) (1,2,3) (1,3,2)   (2,3)   (1,2)      ()
sage: triangle.cayley_table(names=['id','u1','u3','r1','r2','u2'])
 *  id u1 u3 r1 r2 u2
  +------------------
id| id u1 u3 r1 r2 u2
u1| u1 id r1 u3 u2 r2
u3| u3 r2 id u2 u1 r1
r1| r1 u2 u1 r2 id u3
r2| r2 u3 u2 id r1 u1
u2| u2 r1 r2 u1 u3 id
\end{sageexample}
%
You should verify that the table above is correct, just like Table 3.2 is correct.  Remember that the convention is to multiply a row label times a column label, in that order.  However, to do a check across the two tables, you will need to recall the difference in ordering between your textbook and Sage.
%
\sagesubsection{Quaternions}
%
Sage implements the quaternions, but the elements are not matrices, but rather are permutations.  Despite appearances the structure is identical.  It should not matter which version you have in mind (matrices or permutations) if you build the Cayley table and use the default behavior of using letters to name the elements.  As permutations, or as letters, can you identify $-1$, $I$, $J$ and $K$?
%
\begin{sageexample}
sage: Q = QuaternionGroup()
sage: [[a(x) for x in Q.domain()] for a in Q]
[[1, 2, 3, 4, 5, 6, 7, 8], [2, 3, 4, 1, 6, 7, 8, 5],
 [3, 4, 1, 2, 7, 8, 5, 6], [4, 1, 2, 3, 8, 5, 6, 7],
 [5, 8, 7, 6, 3, 2, 1, 4], [6, 5, 8, 7, 4, 3, 2, 1],
 [7, 6, 5, 8, 1, 4, 3, 2], [8, 7, 6, 5, 2, 1, 4, 3]]
sage: Q.cayley_table()
*  a b c d e f g h
 +----------------
a| a b c d e f g h
b| b c d a h e f g
c| c d a b g h e f
d| d a b c f g h e
e| e f g h c d a b
f| f g h e b c d a
g| g h e f a b c d
h| h e f g d a b c
\end{sageexample}
%
It should be fairly obvious that \verb?a? is the identity element of the group ($1$), either from its behavior in the table, or from its ``bottom row'' representation in the list above.  And if you prefer, you can ask Sage.
%
\begin{sageexample}
sage: id = Q.identity()
sage: [id(x) for x in Q.domain()]
[1, 2, 3, 4, 5, 6, 7, 8]
\end{sageexample}
%
Now $-1$ should have the property that $-1\cdot -1= 1$.  We see that the identity element \verb?a? is on the diagonal of the Cayley table only when we compute \verb?c*c?.  We can verify this easily, borrowing the third ``bottom row'' element from the list above.  With this information, once we locate $I$, we can easily compute $-I$, and so on.
%
\begin{sageexample}
sage: minus_one = Q([3, 4, 1, 2, 7, 8, 5, 6])
sage: minus_one*minus_one == Q.identity()
True
\end{sageexample}
%
See if you can pair up the letters with all eight elements of the quaternions.  Be a bit careful with your names, the symbol \verb?I? is used by Sage for the imaginary number $i$ (which we will use below), but Sage will silently let you redefine it to be anything you like.  Same goes for lower-case \verb?i?.  So call your elements of the quaternions something like \verb?QI, QJ, QK? to avoid confusion.\par
%
As we begin to work with groups it is instructive to work with the actual elements.  But many properties of groups are totally independent of the order we use for multiplication, or the names or representations we use for the elements.  Here are facts about the quaternions we can compute without any knowledge of just how the elements are written or multiplied.
%
\begin{sageexample}
sage: Q.is_finite()
True
sage: Q.order()
8
sage: Q.is_abelian()
False
\end{sageexample}
%
\sagesubsection{Subgroups}
%
The best techniques for creating subgroups will come in future chapters, but we can create some groups that are naturally subgroups of other groups.\par
%
Elements of the quaternions were represented by certain permutations of the integers 1 through 8.  We can also build the group of \emph{all} permutations of these eight integers.  It gets pretty big, so do not list it unless you want a lot of output!  (I dare you.)
%
\begin{sageexample}
sage: S8 = SymmetricGroup(8)
sage: a = S8.random_element()
sage: [a(x) for x in S8.domain()]     # random
[5, 2, 6, 4, 1, 8, 3, 7]
sage: S8.order()
40320
\end{sageexample}
%
The quaternions, \verb?Q?, is a subgroup of the full group of all permutations, the symmetric group $S_8$ or \verb?S8?, and Sage regards this as a property of \verb?Q?.
%
\begin{sageexample}
sage: Q.is_subgroup(S8)
True
\end{sageexample}
%
In Sage the complex numbers are known by the name \verb?CC?.  We can create a list of the elements in the subgroup described in Example~\ref{example:groups:C_star}.  Then we can verify that this set is a subgroup by examining the Cayley table, using multiplication as the operation.
%
\begin{sageexample}
sage: H = [CC(1), CC(-1), CC(I), CC(-I)]
sage: CC.multiplication_table(elements=H,
...                           names=['1', '-1', 'i', '-i'])
 *   1 -1  i -i
  +------------
 1|  1 -1  i -i
-1| -1  1 -i  i
 i|  i -i -1  1
-i| -i  i  1 -1
\end{sageexample}
%
\begin{sageverbatim}
\end{sageverbatim}
%











