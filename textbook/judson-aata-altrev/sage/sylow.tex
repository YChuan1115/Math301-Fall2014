%%%%(c)
%%%%(c)  This file is a portion of the source for the textbook
%%%%(c)
%%%%(c)    Abstract Algebra: Theory and Applications
%%%%(c)    by Thomas W. Judson
%%%%(c)
%%%%(c)    Sage Material
%%%%(c)    Copyright 2011 by Robert A. Beezer
%%%%(c)
%%%%(c)  See the file COPYING.txt for copying conditions
%%%%(c)
%%%%(c)
\sagesubsection{Sylow Subgroups}
%
The Sage permutation group method \verb?.sylow_subgroup(p)? will return a single Sylow $p$-subgroup.  If the prime is not a proper divisor of the group order it returns a subgroup of order $p^0$, in other words, a trivial subgroup.  So be careful about the primes you choose.  Sometimes, you may only want \emph{one} such Sylow subgroup, since any two Sylow $p$-subgroups are conjugate, and hence isomorphic (Theorem~\ref{sylow:Sylow_two_theorem}).  This also means we can create other Sylow $p$-subgroups by conjugating the one we have.  The permutation group method \verb?.conjugate(g)? will conjugate the group by \verb?g?.\par
%
With repeated conjugations of a single Sylow $p$-subgroup, we will likely create duplicate subgroups.  So we need to use a slightly complicated construction to form a list of just the unique subgroups in the list of conjugates.  The list comprehension will modify the list of unique subgroups, but also create some output we do not care about, so we assign the unwanted output to the variable \verb?junk?.\par
%
Lets investigate the Sylow subgroups of the dihedral group $D_{18}$.  As a group of order $36=2^2\cdot 3^2$, we know by the First Sylow Theorem that there is a Sylow $2$-subgroup of order $4$ and a Sylow $3$-subgroup of order $9$.  First for $p=2$, we obtain one Sylow $2$-subgroup, form all the conjugates, and form a list of non-duplicate subgroups.  (These commands take a while to execute, so be patient.)
%
\begin{sageexample}
sage: G = DihedralGroup(18)
sage: S2 = G.sylow_subgroup(2); S2
Subgroup of (Dihedral group of order 36 as a permutation group) 
generated by 
[(2,18)(3,17)(4,16)(5,15)(6,14)(7,13)(8,12)(9,11), 
(1,10)(2,11)(3,12)(4,13)(5,14)(6,15)(7,16)(8,17)(9,18)]
sage: allS2 = [S2.conjugate(g) for g in G]
sage: uniqS2 = []
sage: junk = [uniqS2.append(H) for H in allS2 if not H in uniqS2]
sage: uniqS2
[Permutation Group with generators 
[(2,18)(3,17)(4,16)(5,15)(6,14)(7,13)(8,12)(9,11), 
(1,10)(2,11)(3,12)(4,13)(5,14)(6,15)(7,16)(8,17)(9,18)], 
Permutation Group with generators 
[(1,3)(4,18)(5,17)(6,16)(7,15)(8,14)(9,13)(10,12), 
(1,10)(2,11)(3,12)(4,13)(5,14)(6,15)(7,16)(8,17)(9,18)], 
Permutation Group with generators 
[(1,5)(2,4)(6,18)(7,17)(8,16)(9,15)(10,14)(11,13), 
(1,10)(2,11)(3,12)(4,13)(5,14)(6,15)(7,16)(8,17)(9,18)], 
Permutation Group with generators 
[(1,7)(2,6)(3,5)(8,18)(9,17)(10,16)(11,15)(12,14), 
(1,10)(2,11)(3,12)(4,13)(5,14)(6,15)(7,16)(8,17)(9,18)], 
Permutation Group with generators 
[(1,9)(2,8)(3,7)(4,6)(10,18)(11,17)(12,16)(13,15), 
(1,10)(2,11)(3,12)(4,13)(5,14)(6,15)(7,16)(8,17)(9,18)], 
Permutation Group with generators 
[(1,10)(2,11)(3,12)(4,13)(5,14)(6,15)(7,16)(8,17)(9,18), 
(1,11)(2,10)(3,9)(4,8)(5,7)(12,18)(13,17)(14,16)], 
Permutation Group with generators 
[(1,10)(2,11)(3,12)(4,13)(5,14)(6,15)(7,16)(8,17)(9,18), 
(1,13)(2,12)(3,11)(4,10)(5,9)(6,8)(14,18)(15,17)], 
Permutation Group with generators 
[(1,10)(2,11)(3,12)(4,13)(5,14)(6,15)(7,16)(8,17)(9,18), 
(1,15)(2,14)(3,13)(4,12)(5,11)(6,10)(7,9)(16,18)], 
Permutation Group with generators 
[(1,10)(2,11)(3,12)(4,13)(5,14)(6,15)(7,16)(8,17)(9,18), 
(1,17)(2,16)(3,15)(4,14)(5,13)(6,12)(7,11)(8,10)]]
sage: len(uniqS2)
9
\end{sageexample}
%
The Third Sylow Theorem tells us that for $p=2$ we would expect $1, 3\text{ or }9$ Sylow $2$-subgroups, so our computational result of 9 subgroups is consistent with what the theory predicts.  Can you visualize each of these subgroups as symmetries of an $18$-gon?  Notice that we also have many subgroups of order $2$ inside of these subgroups of order $4$.
%
\begin{sageexample}
sage: G = DihedralGroup(18)
sage: S3 = G.sylow_subgroup(3); S3
Subgroup of (Dihedral group of order 36 as a permutation group) 
generated by 
[(1,7,13)(2,8,14)(3,9,15)(4,10,16)(5,11,17)(6,12,18), 
(1,15,11,7,3,17,13,9,5)(2,16,12,8,4,18,14,10,6)]
sage: allS3 = [S3.conjugate(g) for g in G]
sage: uniqS3 = []
sage: junk = [uniqS3.append(H) for H in allS3 if not H in uniqS3]
sage: uniqS3
[Permutation Group with generators 
[(1,7,13)(2,8,14)(3,9,15)(4,10,16)(5,11,17)(6,12,18), 
(1,15,11,7,3,17,13,9,5)(2,16,12,8,4,18,14,10,6)]]
sage: len(uniqS3)
1
\end{sageexample}
%
What does the Third Sylow Theorem predict?  Just $1$ or $4$ Sylow $3$-subgroups.  Having found just one subgroup computationally, we know that all of the conjugates of the lone Sylow $3$-subgroup are equal.  In other words, the Sylow $3$-subgroup is normal in $D_{18}$.  Let's check.
%
\begin{sageexample}
sage: S3.is_normal(G)
True
\end{sageexample}
%
At least one of the subgroups of order $3$ contained in this Sylow $3$-subgroup should be obvious by looking at the orders of the generators, and then you may even notice that the generators given could be reduced, and one is a power of the other.
%
\begin{sageexample}
sage: S3.is_cyclic()
True
\end{sageexample}
%
Remember that there are many other subgroups, of other orders.  For example, can you construct a subgroup of order $6=2\cdot 3$ in $D_{18}$?\par
%
\sagesubsection{Normalizers}
%
A new command that is relevant to this section is the construction of a normalizer.  The Sage command \verb?G.normalizer(H)? will return the subgroup of \verb?G? containing elements that normalize the subgroup \verb?H?.  We illustrate its use with the Sylow subgroups from above.
%
\begin{sageexample}
sage: G = DihedralGroup(18)
sage: S2 = G.sylow_subgroup(2)
sage: S3 = G.sylow_subgroup(3)
sage: N2 = G.normalizer(S2); N2
Subgroup of (Dihedral group of order 36 as a permutation group) 
generated by 
[(2,18)(3,17)(4,16)(5,15)(6,14)(7,13)(8,12)(9,11), 
(1,10)(2,11)(3,12)(4,13)(5,14)(6,15)(7,16)(8,17)(9,18)]
sage: N2 == S2
True
sage: N3 = G.normalizer(S3); N3
Subgroup of (Dihedral group of order 36 as a permutation group) 
generated by 
[(2,18)(3,17)(4,16)(5,15)(6,14)(7,13)(8,12)(9,11), 
(1,2)(3,18)(4,17)(5,16)(6,15)(7,14)(8,13)(9,12)(10,11), 
(1,7,13)(2,8,14)(3,9,15)(4,10,16)(5,11,17)(6,12,18), 
(1,15,11,7,3,17,13,9,5)(2,16,12,8,4,18,14,10,6)]
sage: N3 == G
True
\end{sageexample}
%
The normalizer of a subgroup always contains the whole subgroup, so the normalizer of \verb?S2? is as small as possible.  We already knew \verb?S3? is normal in \verb?G?, so it is no surprise that its normalizer is as big as possible --- every element of \verb?G? normalizes \verb?S3?.  Let's compute a normalizer in $D_{18}$ that is more ``interesting.''
%
\begin{sageexample}
sage: G = DihedralGroup(18)
sage: a = G("(1,7,13)(2,8,14)(3,9,15)(4,10,16)(5,11,17)(6,12,18)")
sage: b = G("(1,5)(2,4)(6,18)(7,17)(8,16)(9,15)(10,14)(11,13)")
sage: H = G.subgroup([a, b])
sage: H.order()
6
sage: N = G.normalizer(H)
sage: N
Subgroup of (Dihedral group of order 36 as a permutation group) 
generated by 
[(1,2)(3,18)(4,17)(5,16)(6,15)(7,14)(8,13)(9,12)(10,11), 
(1,5)(2,4)(6,18)(7,17)(8,16)(9,15)(10,14)(11,13), 
(1,13,7)(2,14,8)(3,15,9)(4,16,10)(5,17,11)(6,18,12)]
sage: N.order()
12
\end{sageexample}
%
So for this subgroup of order $6$, the normalizer is strictly bigger than the subgroup, but still strictly smaller than the whole group (and hence not normal in the dihedral group).  Trivially, a subgroup is normal in its normalizer:
%
\begin{sageexample}
sage: H.is_normal(G)
False
sage: H.is_normal(N)
True
\end{sageexample}
%
\sagesubsection{Finite Simple Groups}
%
We saw earlier Sage's permutation group method \verb?.is_simple()?.  Example~\ref{example:sylow:G_pn} tells us that a group of order $64$ is never simple.  The dicyclic group \verb?DiCyclicGroup(16)? is a non-abelian group of $64$, so we can test this method on this group.  It turns out this group has many normal subgroups --- the list will always contain the trivial subgroup and the group itself, so any number exceeding $2$ indicates a non-trivial normal subgroup.
%
\begin{sageexample}
sage: DC=DiCyclicGroup(16)
sage: DC.order()
64
sage: DC.is_simple()
False
sage: ns = DC.normal_subgroups()
sage: len(ns)
9
\end{sageexample}
%
Here is a rather interesting group, one of the 26 sporadic simple groups, known as the Higman-Sims group, $HS$.  The generators used below come from the representation on 100 points in GAP format, available off of \url{http://web.mat.bham.ac.uk/atlas/v2.0/spor/HS/}.  Generators of order 2 and order 5, roughly 44 million elements, but no normal subgroups.  Amazing.
%
\begin{sageexample}
sage: G = SymmetricGroup(100)
sage: a = G([(1,60),  (2,72),  (3,81),  (4,43),  (5,11),  (6,87),
...          (7,34),  (9,63),  (12,46), (13,28), (14,71), (15,42),
...          (16,97), (18,57), (19,52), (21,32), (23,47), (24,54),
...          (25,83), (26,78), (29,89), (30,39), (33,61), (35,56),
...          (37,67), (44,76), (45,88), (48,59), (49,86), (50,74),
...          (51,66), (53,99), (55,75), (62,73), (65,79), (68,82),
...          (77,92), (84,90), (85,98), (94,100)])
sage: b = G([(1,86,13,10,47),  (2,53,30,8,38),
...          (3,40,48,25,17),  (4,29,92,88,43),   (5,98,66,54, 65),
...          (6,27,51,73,24),  (7,83,16,20,28),   (9,23,89,95,61),
...          (11,42,46,91,32), (12,14, 81,55,68), (15,90,31,56,37),
...          (18,69,45,84,76), (19,59,79,35,93),  (21,22,64,39,100),
...          (26,58,96,85,77), (33,52,94,75,44),  (34,62,87,78,50),
...          (36,82,60,74,72), (41,80,70,49,67),  (57,63,71,99,97)])
sage: a.order()
2
sage: b.order()
5
sage: HS = G.subgroup([a, b])
sage: HS.order()
44352000
sage: HS.is_simple()
True
\end{sageexample}
%
We saw this group earlier in the exercises for Chapter~\ref{actions} on group actions, where it was the single non-trivial normal subgroup of the automorphism group of the Higman-Sims graph.
%
\sagesubsection{GAP Console and Interface}
%
This concludes our exclusive study of group theory, though we will be using groups some in the subsequent sections.  As we have remarked, much of Sage's computation with groups is performed by the open source program, ``Groups, Algorithms, and Programming,'' which is better know as simply GAP.  If after this course you outgrow Sage's support for groups, then learning GAP would be your next step as a group theorist. Every copy of Sage includes a copy of GAP and is easy to see which version of GAP is included:
%
\begin{sageexample}
sage: gap.version()
'4.6.4'
\end{sageexample}
%
You can interact with GAP in Sage in several ways. The most direct is by creating a permutation group via Sage's \verb?gap()? command.
%
\begin{sageexample}
sage: G = gap('Group( (1,2,3,4,5,6), (1,3,5) )')
sage: G
Group( [ (1,2,3,4,5,6), (1,3,5) ] )
\end{sageexample}
%
Now we can use most any GAP command with \verb?G?, via the convention that most GAP commands expect a group as the first argument, and we instead provide the group by using the \verb?G.? syntax.  If you consult the GAP documentation you will see that \verb?Center? is a GAP command that expects a group as its lone argument, and \verb?Centralizer? is a GAP command that expects two arguments --- a group and then a group element.
%
\begin{sageexample}
sage: G.Center()
Group( [ ( 1, 3, 5)( 2, 4, 6) ] )
sage: G.Centralizer('(1, 3, 5)')
Group( [ (1,3,5), (2,4,6), (1,3,5)(2,4,6) ] )
\end{sageexample}
%
In a worksheet you can set the first line of a compute cell to \verb?%gap? and the entire cell will be interpreted as if you were interacting directly with GAP.  This means you would now have to use GAP's syntax.  You can also use the drop-down box at the top of a worksheet, and select \verb?gap? as the system (rather than \verb?sage?) and the whole worksheet will be interpreted as GAP commands.  Here is one simple example, which you should be able to evaluate in your notebook.
%
\begin{sageverbatim}
%gap
G := Group( (1,2,3,4,5,6), (1,3,5) );
Centralizer(G, (1,3,5));
\end{sageverbatim}
%
Notice that
%
\begin{enumerate}
\item We do not need to wrap the individual permutations in as many quotation marks as we do in Sage.
\item Assignment is \verb?:=? not \verb?=?.  If you forget the colon, you will get an error message such as \texttt{Variable: 'G' must have a value}.
\item A line \emph{must} end with a semi-colon.  If you forget, several lines will be merged together.
\end{enumerate}
%
You can get help about GAP commands with a command such as the following, though you will soon see that GAP assumes you know a lot more algebra than Sage assumes you know.
%
\begin{sageverbatim}
print gap.help('SymmetricGroup', pager=False)
\end{sageverbatim}
%
In the command-line version of Sage, you can also use the GAP ``console.''  Again, you need to use GAP syntax, and you do not have many of the conveniences of the Sage notebook.  It is also good to know in advance that \verb?quit;? is how you can leave the GAP console and get back to Sage.  If you run Sage at the command-line, use the command \verb?gap_console()? to start GAP running.\par
%
It is a comfort to know that with Sage you get a complete copy of GAP, installed and all ready to run.  However, this is not a tutorial on GAP, so consult the documentation available at the main GAP website: \url{www.gap-system.org} to learn how to get the most out of it.
%
\begin{sageverbatim}
\end{sageverbatim}
%

