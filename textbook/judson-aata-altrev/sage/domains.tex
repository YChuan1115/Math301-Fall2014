%%%%(c)
%%%%(c)  This file is a portion of the source for the textbook
%%%%(c)
%%%%(c)    Abstract Algebra: Theory and Applications
%%%%(c)    by Thomas W. Judson
%%%%(c)
%%%%(c)    Sage Material
%%%%(c)    Copyright 2011 by Robert A. Beezer
%%%%(c)
%%%%(c)  See the file COPYING.txt for copying conditions
%%%%(c)
%%%%(c)
We have already seen some integral domains and unique factorizations in the previous two chapters.  In addition to what we have already seen, Sage has support for some of the topics from this section, but the coverage is limited.  Some functions will work for some rings and not others, while some functions are not yet part of Sage.  So we will give some examples, but this is far from comprehensive.
%
\sagesubsection{Field of Fractions}
%
Sage is frequently able to construct a field of fractions, or identify a certain field as the field of fractions.  For example, the ring of integers and the field of rational numbers are both implemented in Sage, and the integers ``know'' that the rationals is it's field of fractions.
%
\begin{sageexample}
sage: Q = ZZ.fraction_field(); Q
Rational Field
sage: Q == QQ
True
\end{sageexample}
%
In other cases Sage will construct a fraction field, in the spirit of Lemma~\ref{domains:field_fractions_lemma}.  So it is then possible to do basic calculations in the constructed field.
%
\begin{sageexample}
sage: R.<x> = ZZ[]
sage: P = R.fraction_field();P
Fraction Field of Univariate Polynomial Ring in x over Integer Ring
sage: f = P((x^2+3)/(7*x+4))
sage: g = P((4*x^2)/(3*x^2-5*x+4))
sage: h = P((-2*x^3+4*x^2+3)/(x^2+1))
sage: ((f+g)/h).numerator()
3*x^6 + 23*x^5 + 32*x^4 + 8*x^3 + 41*x^2 - 15*x + 12
sage: ((f+g)/h).denominator()
-42*x^6 + 130*x^5 - 108*x^4 + 63*x^3 - 5*x^2 + 24*x + 48
\end{sageexample}
%
\sagesubsection{Prime Subfields}
%
Corollary~\ref{domains:char_p_Zp_corollary} says every field of characteristic $p$ has a subfield isomorphic to ${\mathbb Z}_p$.  For a finite field, the exact nature of this subfield is not a surprise, but Sage will allow us to extract it easily.
%
\begin{sageexample}
sage: F.<c> = FiniteField(3^5)
sage: F.characteristic()
3
sage: G = F.prime_subfield(); G
Finite Field of size 3
sage: G.list()
[0, 1, 2]
\end{sageexample}
%
More generally, the fields mentioned in the conclusions of Corollary~\ref{domains:char_zero_rationals_corollary} and Corollary~\ref{domains:char_p_Zp_corollary} are known as the ``prime subfield'' of the ring containing them.  Here is an example of the characteristic zero case.
%
\begin{sageexample}
sage: K.<y>=QuadraticField(-7); K
Number Field in y with defining polynomial x^2 + 7
sage: K.prime_subfield()
Rational Field
\end{sageexample}
%
In a rough sense, every characteristic zero field contains a copy of the rational numbers (the fraction field of the integers), which can explain Sage's extensive support for rings and fields that extend the integers and the rationals.
%
\sagesubsection{Integral Domains}
%
Sage can determine if some rings are integral domains and we can test products in them.  However, notions of units, irreducibles or prime elements are not generally supported (outside of what we have seen for polynomials in the previous chapter).  Worse, the construction below creates a ring within a larger field and so some functions (such as \verb?.is_unit()?) pass through and give misleading results.  This is because the construction below creates a ring known as an ``order in a number field.''\par
%
Note also in the following example that ${\mathbb Z}[\sqrt{3}i]$ is built with \emph{two} generators, and there is no easy way to get the internal and external names of the generators to synchronize.
%
\begin{sageexample}
sage: K.<x,y> = ZZ[sqrt(-3)]; K
Order in Number Field in a with defining polynomial x^2 + 3
sage: K.is_integral_domain()
True
sage: K.gens()
[1, a]
sage: (x, y)
(1, a)
sage: (1+y)*(1-y) == 2*2
True
\end{sageexample}
%
The following is a bit misleading, since $4$, as an element of ${\mathbb Z}[\sqrt{3}i]$ does not have a multiplicative inverse, though seemingly we can compute one.
%
\begin{sageexample}
sage: four = K(4)
sage: four.is_unit()
False
sage: four^-1
1/4
\end{sageexample}
%
\sagesubsection{Principal Ideals}
%
When a ring is a principal ideal domain, such as the integers, or polynomials over a field, Sage works well.  Beyond that, support begins to weaken.
%
\begin{sageexample}
sage: T.<x>=ZZ[]
sage: T.is_integral_domain()
True
sage: J = T.ideal(5, x); J
Ideal (5, x) of Univariate Polynomial Ring in x over Integer Ring
sage: Q = T.quotient(J); Q
Quotient of Univariate Polynomial Ring in x over
Integer Ring by the ideal (5, x)
\end{sageexample}
%
\begin{sageexample}
sage: J.is_principal()
Traceback (most recent call last):
...
NotImplementedError
sage: Q.is_field()
Traceback (most recent call last):
...
NotImplementedError
\end{sageexample}
%







