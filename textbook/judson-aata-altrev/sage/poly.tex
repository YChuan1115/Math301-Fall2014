%%%%(c)
%%%%(c)  This file is a portion of the source for the textbook
%%%%(c)
%%%%(c)    Abstract Algebra: Theory and Applications
%%%%(c)    by Thomas W. Judson
%%%%(c)
%%%%(c)    Sage Material
%%%%(c)    Copyright 2011 by Robert A. Beezer
%%%%(c)
%%%%(c)  See the file COPYING.txt for copying conditions
%%%%(c)
%%%%(c)
Sage is particularly adept at building, analyzing and manipulating polynomial rings.  We have seen some of this in the previous chapter.  Let's begin by creating three polynomial rings and checking some of their basic properties.  There are several ways to construct polynomial rings, but the syntax used here is the most straightforward.
%
\sagesubsection{Polynomial Rings and their Elements}
%
\begin{sageexample}
sage: x, y, z = var('x y z')
sage: R.<x> = Integers(8)[]; R
Univariate Polynomial Ring in x over Ring of integers modulo 8
sage: S.<y> = ZZ[]; S
Univariate Polynomial Ring in y over Integer Ring
sage: T.<z> = QQ[]; T
Univariate Polynomial Ring in z over Rational Field
\end{sageexample}
%
Basic properties of rings are availble for these examples.
%
\begin{sageexample}
sage: R.is_finite()
False
sage: R.is_integral_domain()
False
sage: S.is_integral_domain()
True
sage: T.is_field()
False
sage: R.characteristic()
8
sage: T.characteristic()
0
\end{sageexample}
%
With the construction syntax used above, the variables can be used to create elements of the polynomial ring without explicit coercion (though we need to be careful about constant polynomials).
%
\begin{sageexample}
sage: y in S
True
sage: x in S
False
sage: q = (3/2) + (5/4)*z^2
sage: q in T
True
sage: 3 in S
True
sage: r = 3
sage: r.parent()
Integer Ring
sage: s = 3*y^0
sage: s.parent()
Univariate Polynomial Ring in y over Integer Ring
\end{sageexample}
%
Polynomials can be evaluated like they are functions, so we can mimic the evaluation homomorphism.
%
\begin{sageexample}
sage: p = 3 + 5*x + 2*x^2
sage: p.parent()
Univariate Polynomial Ring in x over Ring of integers modulo 8
sage: p(1)
2
sage: [p(t) for t in Integers(8)]
[3, 2, 5, 4, 7, 6, 1, 0]
\end{sageexample}
%
Notice that \verb?p? is a degree two polynomial, yet through a brute-force examination we see that the polynomial only has one root, contrary to our usual expectations.  It can be even more unusual.
%
\begin{sageexample}
sage: q = 4*x^2+4*x
sage: [q(t) for t in Integers(8)]
[0, 0, 0, 0, 0, 0, 0, 0]
\end{sageexample}
%
Sage can create and manipulate rings of polynomials in more than one variable, though we will not have much occasion to use this functionality in this course.
%
\begin{sageexample}
sage: s, t = var('s t')
sage: M.<s, t> = QQ[]; M
Multivariate Polynomial Ring in s, t over Rational Field
\end{sageexample}
%
\sagesubsection{Irreducible Polynomials}
%
Whether or not a polynomial factors, taking into consideration the ring used for its coefficients, is an important topic in this chapter and many of the following chapters.  Sage can factor, and determine irreducibility, over the integers, the rationals, and finite fields.\par
%
First, over the rationals.
%
\begin{sageexample}
sage: x = var('x')
sage: R.<x> = QQ[]
sage: p = 1/4*x^4 - x^3 + x^2 - x - 1/2
sage: p.is_irreducible()
True
sage: p.factor()
(1/4) * (x^4 - 4*x^3 + 4*x^2 - 4*x - 2)
sage: q = 2*x^5 + 5/2*x^4 + 3/4*x^3 - 25/24*x^2 - x - 1/2
sage: q.is_irreducible()
False
sage: q.factor()
(2) * (x^2 + 3/2*x + 3/4) * (x^3 - 1/4*x^2 - 1/3)
\end{sageexample}
%
Factoring over the integers is really no different than factoring over the rationals.  This is the content of Theorem~\ref{poly:Gauss_lemma} --- finding a factorization over the integers can be converted to finding a factorization over the rationals.  So it is with Sage, there is little difference between working over the rationals and the integers.  It is a little different working over a finite field.  Commentary follows.
%
\begin{sageexample}
sage: F.<a> = FiniteField(5^2)
sage: S.<y> = F[]
sage: p = 2*y^5 + 2*y^4 + 4*y^3 + 2*y^2 + 3*y + 1
sage: p.is_irreducible()
True
sage: p.factor()
(2) * (y^5 + y^4 + 2*y^3 + y^2 + 4*y + 3)
sage: q = 3*y^4+2*y^3-y+4; q.factor()
(3) * (y^2 + (a + 4)*y + 2*a + 3) * (y^2 + 4*a*y + 3*a)
sage: r = y^4+2*y^3+3*y^2+4; r.factor()
(y + 4) * (y^3 + 3*y^2 + y + 1)
sage: s = 3*y^4+2*y^3-y+3; s.factor()
(3) * (y + 1) * (y + 3) * (y + 2*a + 4) * (y + 3*a + 1)
\end{sageexample}
%
To check these factorizations, we need to compute in the finite field, \verb?F?, and so we need to know how the symbol \verb?a? behaves.  This symbol is considered as a root of a degree two polynomial over the integers mod 5, which we can get with the \verb?.modulus()? method.
%
\begin{sageexample}
sage: F.modulus()
x^2 + 4*x + 2
\end{sageexample}
%
So $a^2+4a+2=0$, or $a^2=-4a-3=a+2$.  So when checking the factorizations, anytime you see an $a^2$ you can replace it by $a+2$.  Notice that by Corollary~\ref{poly:factor_corollary} we could find the one linear factor of \verb?r?, and the four linear factors of \verb?s?, through a brute-force search for roots.  This is feasible because the field is finite.
%
\begin{sageexample}
sage: [t for t in F if r(t)==0]
[1]
sage: [t for t in F if s(t)==0]
[2, 3*a + 1, 4, 2*a + 4]
\end{sageexample}
%
However, \verb?q? factors into a pair of degree 2 polynomials, so no amount of testing for roots will discover a factor.
%
With Eisenstein's Criterion, we can create irreducible polynomials, such as in Example~\ref{example:poly:poly_eisen}.
%
\begin{sageexample}
sage: W.<w> = QQ[]
sage: p = 16*w^5 - 9*w^4 +3*w^2 + 6*w -21
sage: p.is_irreducible()
True
\end{sageexample}
%
Over the field ${\mathbb Z}_p$, the field of integers mod a prime $p$, Conway polynomials are canonical choices of a polynomial of degree $n$ that is irreducible over ${\mathbb Z}_p$.  See the exercise for more about these polynomials.
%
\sagesubsection{Polynomials over Fields}
%
If $F$ is a field, then every ideal of $F[x]$ is principal (Theorem~\ref{poly:PI_theorem}).  Nothing stops you from giving Sage two (or more) generators to construct an ideal, but Sage will determine the element to use in a description of the ideal as a principal ideal.
%
\begin{sageexample}
sage: W.<w> = QQ[]
sage: r = -w^5 + 5*w^4 - 4*w^3 + 14*w^2 - 67*w + 17
sage: s = 3*w^5 - 14*w^4 + 12*w^3 - 6*w^2 + w
sage: S = W.ideal(r, s)
sage: S
Principal ideal (w^2 - 4*w + 1) of
Univariate Polynomial Ring in w over Rational Field
sage: (w^2)*r + (3*w-6)*s in S
True
\end{sageexample}
%
Theorem~\ref{poly:max_ideal_theorem} is the key fact that allows us to easily construct finite fields.  Here is a construction of a finite field of order $7^5=16\,807$.  All we need is a polynomial of degree 5 that is irreducible over ${\mathbb Z}_7$.
%
\begin{sageexample}
sage: F = Integers(7)
sage: R.<x> = F[]
sage: p = x^5+ x + 4
sage: p.is_irreducible()
True
sage: id = R.ideal(p)
sage: Q = R.quotient(id); Q
Univariate Quotient Polynomial Ring in xbar over
Ring of integers modulo 7 with modulus x^5 + x + 4
sage: Q.is_field()
True
sage: Q.order() == 7^5
True
\end{sageexample}
%
The symbol \verb?xbar? is a generator of the field, but right now it is not accessible.  \verb?xbar? is the coset $x + \langle x^5+ x + 4\rangle$.  A better construction would include specifying this generator.
%
\begin{sageexample}
sage: Q.gen(0)
xbar
sage: Q.<t> = R.quotient(id); Q
Univariate Quotient Polynomial Ring in t over
Ring of integers modulo 7 with modulus x^5 + x + 4
sage: t^5 + t + 4
0
sage: t^5 == -(t+4)
True
sage: t^5
6*t + 3
sage: (3*t^3 + t + 5)*(t^2 + 4*t + 2)
5*t^4 + 2*t^2 + 5*t + 5
sage: a = 3*t^4 - 6*t^3 + 3*t^2 + 5*t + 2
sage: ainv = a^-1; ainv
6*t^4 + 5*t^2 + 4
sage: a*ainv
1
\end{sageexample}
%
