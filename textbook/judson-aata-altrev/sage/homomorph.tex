%%%%(c)
%%%%(c)  This file is a portion of the source for the textbook
%%%%(c)
%%%%(c)    Abstract Algebra: Theory and Applications
%%%%(c)    by Thomas W. Judson
%%%%(c)
%%%%(c)    Sage Material
%%%%(c)    Copyright 2011 by Robert A. Beezer
%%%%(c)
%%%%(c)  See the file COPYING.txt for copying conditions
%%%%(c)
%%%%(c)
Sage is able to create homomorphisms (and by extension, isomorphisms and automorphisms) between finite permutation groups.  There is a limited supply of commands then available to manipulate these functions, but we can still illustrate many of the ideas in this chapter.
%
\sagesubsection{Homomorphisms}
%
The principal device for creating a homomorphism is to specify the specific images of the set of generators for the domain.  Consider cyclic groups of order 12 and 20:
%
\begin{align*}
G &= \{a^i\vert a^{12}=e\}
&
H &= \{x^i\vert x^{20}=e\}
\end{align*}
%
and define a homomorphism by just defining the image of the generator of $G$, and define the rest of the mapping by extending the mapping via the operation-preserving property of a homomorphism.
%
\begin{align*}
\phi: G\rightarrow H, &\quad\phi(a)=x^5\\
\Rightarrow           &\quad\phi(a^i) = \phi(a)^i = (x^5)^i = x ^{5i}
\end{align*}
%
The constructor \verb?PermutationGroupMorphism? requires the two groups, then a list of images for each generator (in order!), and then will create the homomorphism.  Note that we can then use the result as a function.  In the example below, we first verify that \verb?C12? has a single generator (no surprise there), which we then send to a particular element of order 4 in the codomain.  Sage then constructs the unique homomorphism that is consistent with this requirement.
%
\begin{sageexample}
sage: C12 = CyclicPermutationGroup(12)
sage: C20 = CyclicPermutationGroup(20)
sage: domain_gens = C12.gens()
sage: [g.order() for g in domain_gens]
[12]
sage: x = C20.gen(0)
sage: y = x^5
sage: y.order()
4
sage: phi = PermutationGroupMorphism(C12, C20, [y])
sage: phi
Permutation group morphism:
  From: Cyclic group of order 12 as a permutation group
  To:   Cyclic group of order 20 as a permutation group
  Defn: [(1,2,3,4,5,6,7,8,9,10,11,12)] ->
        [(1,6,11,16)(2,7,12,17)(3,8,13,18)(4,9,14,19)(5,10,15,20)]
sage: a = C12("(1,6,11,4,9,2,7,12,5,10,3,8)")
sage: phi(a)
(1,6,11,16)(2,7,12,17)(3,8,13,18)(4,9,14,19)(5,10,15,20)
sage: b = C12("(1,3,5,7,9,11)(2,4,6,8,10,12)")
sage: phi(b)
(1,11)(2,12)(3,13)(4,14)(5,15)(6,16)(7,17)(8,18)(9,19)(10,20)
sage: c = C12("(1,9,5)(2,10,6)(3,11,7)(4,12,8)")
sage: phi(c)
()
\end{sageexample}
%
Note that the element \verb?c? must therefore be in the kernel of \verb?phi?.\par
%
We can then compute the subgroup of the domain that is the kernel, in this case a cyclic group of order 3 inside the cyclic group of order 12.  We can compute the image of \emph{any} subgroup, but here we will build the whole homomorphic image by supplying the whole domain to the \verb?.image()? method.  Here the image is a cyclic subgroup of order 4 inside the cyclic group of order 20.  Then we can verify the First Isomorphism Theorem.
%
\begin{sageexample}
sage: K = phi.kernel(); K
Subgroup of (Cyclic group of order 12 as a permutation group) 
generated by [(1,5,9)(2,6,10)(3,7,11)(4,8,12)]
sage: Im = phi.image(C12); Im
Subgroup of (Cyclic group of order 20 as a permutation group) 
generated by [(1,6,11,16)(2,7,12,17)(3,8,13,18)(4,9,14,19)(5,10,15,20)]
sage: Im.is_isomorphic(C12.quotient(K))
True
\end{sageexample}
%
Here is a slightly more complicated example.  The dihedral group $D_{20}$ is the symmetry group of a $20$-gon.  Inside this group is a subgroup that is isomorphic to the symmetry group of a $5$-gon (pentagon).  Is this a surprise, or is this obvious?  Here is a way to make precise the statement ``$D_{20}$ contains a copy of $D_{5}$.''\par
%
We build the domain and find its generators, so we know how many images to supply in the definition of the homomorphism.  Then we construct the codomain, from which we will construct images.  Our choice here is to send a reflection to a reflection, and a rotation to a rotation.  But the rotations will both have order 5, and both are a rotation by $\frac{2\pi}{5}$ radians.
%
\begin{sageexample}
sage: G = DihedralGroup(5)
sage: H = DihedralGroup(20)
sage: G.gens()
[(1,2,3,4,5), (1,5)(2,4)]
sage: H.gens()
[(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20),
 (1,20)(2,19)(3,18)(4,17)(5,16)(6,15)(7,14)(8,13)(9,12)(10,11)]
sage: x = H.gen(0)^4
sage: y = H.gen(1)
sage: rho = PermutationGroupMorphism(G, H, [x, y])
sage: rho.kernel()
Subgroup of (Dihedral group of order 10 as a permutation group) 
generated by [()]
\end{sageexample}
%
Since the kernel is trivial, \verb?rho? is a one-to-one function (see Problem 19).  But more importantly, by the First Isomorphishm Theorem, \verb?G? is isomorphic to the image of the homomorphism.  We compute the image and check the claim.
%
\begin{sageexample}
sage: Im = rho.image(G); Im
Subgroup of (Dihedral group of order 40 as a permutation group) 
generated by 
[(1,5,9,13,17)(2,6,10,14,18)(3,7,11,15,19)(4,8,12,16,20), 
(1,20)(2,19)(3,18)(4,17)(5,16)(6,15)(7,14)(8,13)(9,12)(10,11)]
sage: Im.is_subgroup(H)
True
sage: Im.is_isomorphic(G)
True
\end{sageexample}
%
Just providing a list of images for the generators of the domain is no guarantee that the function will extend to a homomorphism.  For starters, the order of each image must divide the order of the corresponding preimage.  (Can you prove this?)  And similarly, if the domain is abelian, then the image must also be abelian, so in this case the list of images should not generate a non-abelian subgroup.  Here is an example.  There are no homomorphisms from a cyclic group of order 7 to a cyclic group of order 4 (other than the trivial function that takes every element to the identity).  To see this, consider the possible orders of the kernel, and of the two possibilities, see that one is impossible and the other arises with the trivial homomorphism.  Unfortunately, Sage acts as if nothing is wrong in creating a homomorphism between these groups, but what Sage builds is useless and raises errors when you try to use it.
%
\begin{sageexample}
sage: G = CyclicPermutationGroup(7)
sage: H = CyclicPermutationGroup(4)
sage: tau = PermutationGroupMorphism_im_gens(G, H, H.gens())
sage: tau
Permutation group morphism:
  From: Cyclic group of order 7 as a permutation group
  To:   Cyclic group of order 4 as a permutation group
  Defn: [(1,2,3,4,5,6,7)] -> [(1,2,3,4)]
sage: tau.kernel()
Traceback (most recent call last):
...
RuntimeError: Gap produced error output
...
\end{sageexample}
%
Rather than creating homomorphisms ourselves, in certain situations Sage knows of the existence of natural homomorphisms and will create them for you.  One such case is a direct product construction.  Given a group \verb?G?, the method \verb?.direct_product(H)? will create the direct product $G\times H$.  (This is not the same command as the function \verb?direct_product_permgroups()? from before.)  Not only does this command create the direct product, but it also builds \emph{four} homomorphisms, one with domain $G$, one with domain $H$ and two with domain $G\times H$.  So the output consists of five objects, the first being the actual group, and the remainder are homomorphisms.  We will demonstrate the call here, and leave a more thorough investigation for the exercises.
%
\begin{sageexample}
sage: G = CyclicPermutationGroup(3)
sage: H = DihedralGroup(4)
sage: results = G.direct_product(H)
sage: results[0]
Permutation Group with generators [(4,5,6,7), (4,7)(5,6), (1,2,3)]
sage: results[1]
Permutation group morphism:
  From: Cyclic group of order 3 as a permutation group
  To:   Permutation Group with generators 
        [(4,5,6,7), (4,7)(5,6), (1,2,3)]
  Defn: Embedding( Group( [ (1,2,3), (4,5,6,7), (4,7)(5,6) ] ), 1 )
sage: results[2]
Permutation group morphism:
  From: Dihedral group of order 8 as a permutation group
  To:   Permutation Group with generators 
        [(4,5,6,7), (4,7)(5,6), (1,2,3)]
  Defn: Embedding( Group( [ (1,2,3), (4,5,6,7), (4,7)(5,6) ] ), 2 )
sage: results[3]
Permutation group morphism:
  From: Permutation Group with generators 
        [(4,5,6,7), (4,7)(5,6), (1,2,3)]
  To:   Cyclic group of order 3 as a permutation group
  Defn: Projection( Group( [ (1,2,3), (4,5,6,7), (4,7)(5,6) ] ), 1 )
sage: results[4]
Permutation group morphism:
  From: Permutation Group with generators 
        [(4,5,6,7), (4,7)(5,6), (1,2,3)]
  To:   Dihedral group of order 8 as a permutation group
  Defn: Projection( Group( [ (1,2,3), (4,5,6,7), (4,7)(5,6) ] ), 2 )
\end{sageexample}
%
\begin{sageverbatim}
\end{sageverbatim}
%
