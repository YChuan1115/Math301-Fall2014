%%%%(c)
%%%%(c)  This file is a portion of the source for the textbook
%%%%(c)
%%%%(c)    Abstract Algebra: Theory and Applications
%%%%(c)    by Thomas W. Judson
%%%%(c)
%%%%(c)    Sage Material
%%%%(c)    Copyright 2011 by Robert A. Beezer
%%%%(c)
%%%%(c)  See the file COPYING.txt for copying conditions
%%%%(c)
%%%%(c)
Sage can create all of the cosets of a subgroup, and all of the subgroups of a group.  While these methods can be somewhat slow, they are in many, many ways much better than experimenting with pencil and paper, and can greatly assist us in understanding the structure of finite groups.\par
%
\sagesubsection{Cosets}
%
Sage will create all the right (or left) cosets of a subgroup.  Written mathematically, cosets are sets, and the order of the elements within the set is irrelevant.  With Sage, lists are more natural, and here it is to our advantage.\par
%
Sage creates the cosets of a subgroup as a list of lists.  Each inner list is a single coset.  The first coset is always the coset that is the subgroup itself, and the first element of this coset is the identity.  Each of the other cosets can be construed to have their first element as their representative, and if you use this element as the representative, the elements of the coset are in the same order they would be created by multiplying this representative by the elements of the first coset (the subgroup).\par
%
The keyword \verb?side? can be \verb?'right'? or \verb?'left'?, and if not given, then the default is right cosets.  The options refer to which side of the product has the representative.  Notice that now Sage's results will be ``backwards'' compared with the text.  Here is Example~\ref{example:cosets:S3_Cosets} reprised, but in a slightly different order.
%
\begin{sageexample}
sage: G = SymmetricGroup(3)
sage: a = G("(1,2)")
sage: H = G.subgroup([a])
sage: rc = G.cosets(H, side='right'); rc
[[(), (1,2)], [(2,3), (1,3,2)], [(1,2,3), (1,3)]]
sage: lc = G.cosets(H, side='left'); lc
[[(), (1,2)], [(2,3), (1,2,3)], [(1,3,2), (1,3)]]
\end{sageexample}
%
So if we work our way through the brackets carefully we can see the difference between the right cosets and the left cosets.  Compare these cosets with the ones in the text and see that left and right are reversed.  Shouldn't be a problem --- just keep it in mind.
%
\begin{sageexample}
sage: G = SymmetricGroup(3)
sage: b = G("(1,2,3)")
sage: H = G.subgroup([b])
sage: rc = G.cosets(H, side='right'); rc
[[(), (1,2,3), (1,3,2)], [(2,3), (1,3), (1,2)]]
sage: lc = G.cosets(H, side='left'); lc
[[(), (1,2,3), (1,3,2)], [(2,3), (1,2), (1,3)]]
\end{sageexample}
%
If we study the backeting, we can see that the left and right cosets are equal.  Let's see what Sage thinks:
%
\begin{sageexample}
sage: rc == lc
False
\end{sageexample}
%
Mathematically, we need sets, but Sage is working with ordered lists, and the order matters.  However, if we know our lists do not have duplicates (the \verb?.cosets()? method will never produce duplicates) then we can sort the lists and a test for equality will perform as expected.  The elements of a permutation group have an ordering defined for them --- it is not so important \emph{what} this is, just that \emph{some} ordering is defined.  The \verb?sorted()? function will take any list and return a sorted version.  So for each list of cosets, we will sort the individual cosets and then sort the list of sorted cosets.  This is a typical maneuver, though a bit complicated with the nested lists.
%
\begin{sageexample}
sage: rc_sorted = sorted([sorted(coset) for coset in rc])
sage: rc_sorted
[[(), (1,2,3), (1,3,2)], [(2,3), (1,2), (1,3)]]
sage: lc_sorted = sorted([sorted(coset) for coset in lc])
sage: lc_sorted
[[(), (1,2,3), (1,3,2)], [(2,3), (1,2), (1,3)]]
sage: rc_sorted == lc_sorted
True
\end{sageexample}
%
The list of all cosets can be quite long (it will include every element of the group) and can take a few seconds to complete, even for small groups.  There are more sophisticated, and faster, ways to study cosets (such as just using their representatives), but to understand these techniques you also need to understand more theory.
%
\sagesubsection{Subgroups}
%
Sage can compute all of the subgroups of a group.  This can produce even more output than the coset method and can sometimes take much longer, depending on the structure of the group.  The list is in order of the size of the subgroups, with smallest first.  As a demonstration we will first compute and list all of the subgroups of a small group, and then extract just one of these subgroups from the list for some futher study.
%
\begin{sageexample}
sage: G = SymmetricGroup(3)
sage: sg = G.subgroups(); sg
[Permutation Group with generators [()],
 Permutation Group with generators [(2,3)],
 Permutation Group with generators [(1,2)],
 Permutation Group with generators [(1,3)],
 Permutation Group with generators [(1,2,3)],
 Permutation Group with generators [(2,3), (1,2,3)]]
sage: H = sg[4]; H
Permutation Group with generators [(1,2,3)]
sage: H.order()
3
sage: H.list()
[(), (1,2,3), (1,3,2)]
sage: H.is_cyclic()
True
\end{sageexample}
%
The output of the \verb?.subgroups()? method can be voluminous, so sometimes we are interested in properties of specific subgroups (as in the previous example) or broader questions of the group's ``subgroup structure.''  Here we expand on Corollary~\ref{cosets_theorem_10}.  Notice that just because Sage does not \emph{compute} a subgroup of order 6 in $A_4$, this is no substitute whatsoever for a \emph{proof} such as given for the corollary.  But the computational result emboldens us to search for the theoretical result with confidence.
%
\begin{sageexample}
sage: G = AlternatingGroup(4)
sage: sg = G.subgroups()
sage: [H.order() for H in sg]
[1, 2, 2, 2, 3, 3, 3, 3, 4, 12]
\end{sageexample}
%
So we see no subgroup of oder 6 in the list of subgroups of $A_4$.  Notice how Lagrange's Theorem (Theorem~\ref{LagrangeTheorem} is in evidence --- all the subgroup orders divide $12$, the order of $A_4$.  Be patient, the next subgroup computation may take a while.
%
\begin{sageexample}
sage: G = SymmetricGroup(4)
sage: sg = G.subgroups()
sage: [H.order() for H in sg]
[1, 2, 2, 2, 2, 2, 2, 2, 2, 2, 3, 3, 3, 3, 4, 4, 4, 4, 4, 4, 4,
 6, 6, 6, 6, 8, 8, 8, 12, 24]
\end{sageexample}
%
Again, note Lagrange's Theorem in action.  But more interestingly, $S_4$ has a subgroup of order 6.  Four of them, to be precise.  these four subgroups of order 6 are similar to each other, can you describe them simply (\emph{before} digging into the \verb?sg? list for more information)?  If you were curious how many subgroups $S_4$ has, you could simply count the number of subgroups in the \verb?sg? list.  The \verb?len()? function does this for \emph{any} list and is often an easy way to count things.
%
\begin{sageexample}
sage: len(sg)
30
\end{sageexample}
%
\sagesubsection{Subgroups of Cyclic Groups}
%
Now that we are more familiar with permutation groups, and know about the \verb?.subgroups()? method, we can revisit an idea from Chapter~\ref{cyclic}.  The subgroups of a cyclic group are always cyclic, but how many are there and what are their orders?
%
\begin{sageexample}
sage: G = CyclicPermutationGroup(20)
sage: [H.order() for H in G.subgroups()]
[1, 2, 4, 5, 10, 20]
\end{sageexample}
%
\begin{sageexample}
sage: G = CyclicPermutationGroup(19)
sage: [H.order() for H in G.subgroups()]
[1, 19]
\end{sageexample}
%
We could do this all day, but you have Sage at your disposal, so vary the order of \verb?G? by changing \verb?n? and study the output across many runs.  Maybe try a cyclic group of order 24 and compare with the symmetric group $S_4$ (above) which also has order 24.  Do you feel a conjecture coming on?
%
\begin{sageexample}
sage: n = 8
sage: G = CyclicPermutationGroup(n)
sage: [H.order() for H in G.subgroups()]
[1, 2, 4, 8]
\end{sageexample}
%
\sagesubsection{Euler Phi Function}
%
To add to our number-theoretic functions from Chapter~\ref{integers}, we note that Sage makes the Euler $\phi$-function available as the function \verb?euler_phi()?.
%
\begin{sageexample}
sage: euler_phi(345)
176
\end{sageexample}
%
Here's an interesting experiment that you can try running several times.
%
\begin{sageverbatim}
sage: m = random_prime(10000)
sage: n = random_prime(10000)
sage: m, n, euler_phi(m*n) == euler_phi(m)*euler_phi(n)  # random
(5881, 1277, True)
\end{sageverbatim}
%
Feel another conjecture coming on?  Can you generalize this result?
%
\begin{sageverbatim}
\end{sageverbatim}
%
