%%%%(c)
%%%%(c)  This file is a portion of the source for the textbook
%%%%(c)
%%%%(c)    Abstract Algebra: Theory and Applications
%%%%(c)    by Thomas W. Judson
%%%%(c)
%%%%(c)    Sage Material
%%%%(c)    Copyright 2011 by Robert A. Beezer
%%%%(c)
%%%%(c)  See the file COPYING.txt for copying conditions
%%%%(c)
%%%%(c)
A good portion of Sage's support for group theory is based on routines from GAP (Groups, Algorithms, and Programming) at \url{http://www.gap-system.org/}, which is included in every copy of Sage.  This is a mature open source package, dating back to 1986. (Forward reference here to GAP console, etc.)\par
%
As we have seen, groups can be described in many different ways, such as sets of matrices, sets of complex numbers, or sets of symbols subject to defining relations.  A very concrete
way to represent groups is via permutations (one-to-one and onto functions of the integers $1$ through $n$), using function composition as the operation in the group, as described in this chapter.  Sage has many routines designed to work with groups of this type and they are also a good way for those learning group theory to gain experience with the basic ideas of group theory.  For both these reasons, we will concentrate on these types of groups.
%
\sagesubsection{Permutation Groups and Elements}
%
The easiest way to work with permutation group elements in Sage is to write them in cycle notation.  Since these are products of disjoint cycles (which commute), we do not need to concern ourselves with the actual order of the cycles.  If we write \verb?(1,3)(2,4)? we probably understand it to be a permutation (the topic of this chapter!) and we know that it could be an element of $S_4$, or perhaps a symmetric group on more symbols than just 4.  Sage cannot get started that easily and needs a bit of context, so we coerce a string of characters written with cycle notation into a symmetric group to make group elements.  Here are some examples and some sample computations.  Remember that Sage and your text differ on the interpretation of the product of two permutations.
%
\begin{sageexample}
sage: G = SymmetricGroup(5)
sage: sigma = G("(1,3)(2,5,4)")
sage: sigma*sigma
(2,4,5)
sage: rho = G("(2,4)(1,5)")
sage: rho^3
(1,5)(2,4)
sage: sigma*rho
(1,3,5,2)
sage: rho*sigma
(1,4,5,3)
sage: rho^-1*sigma*rho
(1,2,4)(3,5)
\end{sageexample}
%
There are alternate ways to create permutation group elements, which can be useful in some situations, but they are not quite as useful in everday use.
%
\begin{sageexample}
sage: G = SymmetricGroup(5)
sage: sigma1 = G("(1,3)(2,5,4)")
sage: sigma2 = G([(1,3),(2,5,4)])
sage: sigma3 = G([3,5,1,2,4])
sage: sigma1
(1,3)(2,5,4)
sage: sigma2
(1,3)(2,5,4)
sage: sigma3
(1,3)(2,5,4)
sage: sigma1 == sigma2
True
sage: sigma2 == sigma3
True
sage: sigma2.cycle_tuples()
[(1, 3), (2, 5, 4)]
sage: [sigma3(x) for x in G.domain()]
[3, 5, 1, 2, 4]
\end{sageexample}
%
The second version of $\sigma$ is a list of ``tuples'', which requires a lot of commas and these must be enclosed in a list.  (A tuple of length one must be written like \verb?(4,)? to distinguish it from using parentheses for grouping, as in \verb?5*(4)?.)  The third version uses the ``bottom-row'' of the more cumbersome two-row notation introduced at the beginning of the chapter --- it is an ordered list of the \emph{output values} of the permutation when considered as a function.\par
%
So we then see that despite three different input procedures, all the versions of $\sigma$ print the same way, and moreso they are actually equal to each other.  (This is a subtle difference --- what an object \emph{is} in Sage versus how an object \emph{displays} itself.)\par
%
We can be even more careful about the nature of our elements.  Notice that once we get Sage started, it can promote the product $\tau\sigma$ into the larger permutation group.  We can ``promote'' elements into larger permutation groups, but it is an error to try to shoe-horn an element into a too-small symmetric group.
%
\begin{sageexample}
sage: H = SymmetricGroup(4)
sage: sigma = H("(1,2,3,4)")
sage: G = SymmetricGroup(6)
sage: tau = G("(1,2,3,4,5,6)")
sage: rho = tau * sigma
sage: rho
(1,3)(2,4,5,6)
sage: sigma.parent()
Symmetric group of order 4! as a permutation group
sage: tau.parent()
Symmetric group of order 6! as a permutation group
sage: rho.parent()
Symmetric group of order 6! as a permutation group
sage: tau.parent() == rho.parent()
True
sage: sigmaG = G(sigma)
sage: sigmaG.parent()
Symmetric group of order 6! as a permutation group
\end{sageexample}
%
It is an error to try to coerce a permutation with too many symbols into a permutation group employing too few symbols.
%
\begin{sageexample}
sage: tauH = H(tau)
Traceback (most recent call last):
...
ValueError: Invalid permutation vector: (1,2,3,4,5,6)
\end{sageexample}
%
Better than working with just elements of the symmetric group, we can create a variety of permutation groups in Sage.  Here is a sampling for starters:
%
\begin{center}
\begin{tabular}{ll}
\verb?SymmetricGroup(n)?         & Permutations on $n$ symbols, $n!$ elements.\\
\verb?DihedralGroup(n)?          & Symmetries of an $n$-gon, $2n$ elements.\\
\verb?CyclicPermutationGroup(n)? & Rotations of an $n$-gon (no flips), $n$ elements.\\
\verb?AlternatingGroup(n)?       & Alternating group on $n$ symbols, $n!/2$ elements.\\
\verb?KleinFourGroup()?          & A non-cyclic group of order 4.
\end{tabular}
\end{center}
%
\sagesubsection{Properties of Permutation Elements}
%
Sometimes it is easier to grab an element out of a list of elements of a permutation group, and then it is already attached to a parent and there is no need for any coercion.  In the following, \verb?rotate? and \verb?flip? are automatically elements of \verb?G? because of the way we procured them.
%
\begin{sageexample}
sage: D = DihedralGroup(5)
sage: elements = D.list(); elements
[(), (2,5)(3,4), (1,2)(3,5), (1,2,3,4,5), (1,3)(4,5),
(1,3,5,2,4), (1,4)(2,3), (1,4,2,5,3), (1,5,4,3,2), (1,5)(2,4)]
sage: rotate = elements[3]
sage: flip = elements[1]
sage: flip*rotate == rotate* flip
False
\end{sageexample}
%
So we see from this final statement that the group of symmetries of a pentagon is not abelian.  But there is an easier way.
%
\begin{sageexample}
sage: D = DihedralGroup(5)
sage: D.is_abelian()
False
\end{sageexample}
%
There are many more methods you can use for both permutation groups and their individual elements.  Use the blank compute cell below to create a permutation group (any one you like) and an element of a permutation group (any one you like).  Then use tab-completion to see all the methods available for an element, or for a group (name, period, tab-key).  Some names you may recognize, some we will learn about in the coming chapters, some are highly-specialized research tools you can use when you write your Ph.D.\ thesis in group theory.  For any of these methods, remember that you can type the name, followed by a question mark, to see documentation and examples.  \emph{Experiment and explore} --- it is really hard to break anything.\par
%
\begin{sageverbatim}
\end{sageverbatim}
%
Here are some selected examples of various methods available.
%
\begin{sageexample}
sage: A4 = AlternatingGroup(4)
sage: A4.order()
12
sage: A4.is_finite()
True
sage: A4.is_abelian()
False
sage: A4.is_cyclic()
False
sage: sigma = A4("(1,2,4)")
sage: sigma^-1
(1,4,2)
sage: sigma.order()
3
\end{sageexample}
%
A very useful method when studying the alternating group is the permutation group element method \verb?.sign()?.  It will return \verb?1? if a permutation is even and \verb?-1? if a permutation is odd.
%
\begin{sageexample}
sage: G = SymmetricGroup(3)
sage: sigma = G("(1,2)")
sage: tau = G("(1,3)")
sage: rho = sigma*tau
sage: sigma.sign()
-1
sage: rho.sign()
1
\end{sageexample}
%
We can create subgroups by giving the main group a list of ``generators.''  These elements serve to ``generate'' a subgroup --- imagine multiplying these elements (and their inverses) together over and over, creating new elements that must also be in the subgroup and also become involved in new products, until you see no new elements.  Now that definition ends with a horribly imprecise statement, but it should suffice for now.  A better definition is that the subgroup generated by the elements is the smallest subgroup of the main group that contains all the generators --- which is fine if you know what all the subgroups might be.\par
%
With a single generator, the repeated products just become powers of the lone generator.  The subgroup generated then is cyclic.  With two (or more) generators, especially in a non-abelian group, the situation can be much, much more complicated.  So let's begin with just a single generator.  But don't forget to put it in a list anyway.
%
\begin{sageexample}
sage: A4 = AlternatingGroup(4)
sage: sigma = A4("(1,2,4)")
sage: sg = A4.subgroup([sigma])
sage: sg
Subgroup of (Alternating group of order 4!/2 as a permutation group) 
generated by [(1,2,4)]
sage: sg.order()
3
sage: sg.list()
[(), (1,2,4), (1,4,2)]
sage: sg.is_abelian()
True
sage: sg.is_cyclic()
True
sage: sg.is_subgroup(A4)
True
\end{sageexample}
%
We can now redo the example from the very beginning of this chapter.  We translate to elements to cycle notation, construct the subgroup from two generators (the subgroup is not cyclic), and since the subgroup is abelian, we do not have to view Sage's Cayley table as a diagonal reflection of the table in the example.
%
\begin{sageexample}
sage: G = SymmetricGroup(5)
sage: sigma = G("(4,5)")
sage: tau = G("(1,3)")
sage: H = G.subgroup([sigma, tau])
sage: H.list()
[(), (4,5), (1,3), (1,3)(4,5)]
sage: text_names = ['id', 'sigma', 'tau', 'mu']
sage: H.cayley_table(names=text_names)
    *     id sigma   tau    mu
     +------------------------
   id|    id sigma   tau    mu
sigma| sigma    id    mu   tau
  tau|   tau    mu    id sigma
   mu|    mu   tau sigma    id
\end{sageexample}
%
\sagesubsection{Motion Group of a Cube}
%
We could mimic the example in the text and create elements of $S_4$ as permutations of the diagonals.  A more obvious, but less insightful, construction is to view the 8 corners of the cube as the items being permuted.  Then some obvious symmetries of the cube come from running an axis through the center of a side, through to the center of the obvious side, with quarter-turns or half-turns about these axes forming symmetries.  With three such axes and four rotations per axis, we get 12 symmetries, except we have counted the identity permutation two extra times.\par
%
Label the four corners of the square top with $1$ through $4$ with $1$ in the left-front corner, arranged clockwise when viewed from above.  Use $5$ through $8$ for the bottom square's corner, so that $5$ is below $1$, $6$ below $2$, etc.  We will use quarter-turns, clockwise, around each axis, when viewed from above, the front, and the right.
%
\begin{sageexample}
sage: G = SymmetricGroup(8)
sage: above = G("(1,2,3,4)(5,6,7,8)")
sage: front = G("(1,4,8,5)(2,3,7,6)")
sage: right = G("(1,2,6,5)(3,7,8,4)")
sage: cube = G.subgroup([above, front, right])
sage: cube.order()
24
sage: cube.list()
[(), (2,4,5)(3,8,6), (2,5,4)(3,6,8), (1,2)(3,5)(4,6)(7,8),
(1,2,3,4)(5,6,7,8), (1,2,6,5)(3,7,8,4), (1,3,6)(4,7,5),
(1,3)(2,4)(5,7)(6,8), (1,3,8)(2,7,5), (1,4,3,2)(5,8,7,6),
(1,4,8,5)(2,3,7,6), (1,4)(2,8)(3,5)(6,7), (1,5,6,2)(3,4,8,7),
(1,5,8,4)(2,6,7,3), (1,5)(2,8)(3,7)(4,6), (1,6,3)(4,5,7),
(1,6)(2,5)(3,8)(4,7), (1,6,8)(2,7,4), (1,7)(2,3)(4,6)(5,8),
(1,7)(2,6)(3,5)(4,8), (1,7)(2,8)(3,4)(5,6), (1,8,6)(2,4,7),
(1,8,3)(2,5,7), (1,8)(2,7)(3,6)(4,5)]
\end{sageexample}
%
Since we know from the discussion in the text that the symmetry group has 24 elements, we see that our three quarter-turns are sufficient to create every symmetry.  This prompts several questions:
%
\begin{enumerate}
%
\item  Can you locate the ten rotations about axes?  (Hint: the identity is easy, the other 9 never send any symbol to itself.)
%
\item Can you identify the six symmetries that are a transposition of diagonals?  (Hint: \verb?[g for g in cube if g.order() == 2]? is a good preliminary filter.)
%
\item Verify that any two of our quarter-turns are sufficient to generate the whole group.  How do you know they generate the whole group?
%
\item Can you express one of the diagonal transpositions as a product of quarter-turns?  This can be a notoriously difficult problem, especially for software.  It is known as the ``word problem.''
%
\item Number the six faces of the cube with the numbers $1$ through $6$ (any way you like).  Now consider the same three symmetries we used before (quarter-turns about face-to-face axes) and express them as elements of $S_6$.  Verify that the subgroup generated by these symmetries is the whole group.  Again, rather than using three generators, try using just two.
%
\end{enumerate}
%
\begin{sageverbatim}
\end{sageverbatim}
%

